/*
Copyright (c) Microsoft Open Technologies, Inc.
All Rights Reserved
Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the
License. You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0

THIS CODE IS PROVIDED ON AN *AS IS* BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, EITHER EXPRESS OR IMPLIED,
INCLUDING WITHOUT LIMITATION ANY IMPLIED WARRANTIES OR CONDITIONS OF TITLE, FITNESS FOR A PARTICULAR PURPOSE,
MERCHANTABLITY OR NON-INFRINGEMENT.

See the Apache 2 License for the specific language governing permissions and limitations under the License.
*/

buildscript {
    repositories {
    	jcenter()
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0-rc1'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
    }
}

apply plugin: 'com.android.library'

version = "0.0.2"
group = 'com.msopentech.thali'
archivesBaseName = 'ThaliOnionProxyAndroid'

repositories {
    mavenLocal()
    mavenCentral()
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 16 // Please see the readme if you need Sdk support below 16
        targetSdkVersion 21
    }

    // Needed to work around https://code.google.com/p/android/issues/detail?id=61573#c14
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE.txt' //https://groups.google.com/forum/#!topic/adt-dev/bl5Rc4Szpzg
        exclude 'META-INF/LICENSE.txt'
    }
}

dependencies {
    compile fileTree(dir: '../universal/libs', include: '*.jar')
    compile project(':universal')
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'com.msopentech.thali:ThaliOnionProxyUniversal:' + version

    androidTestCompile 'org.slf4j:slf4j-android:1.7.7'
}

apply plugin: 'com.github.dcendents.android-maven'

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

task deleteSourceFiles(type: Delete) {
    delete 'src/main/assets/geoip'
    delete 'src/main/assets/geoip6'
    delete 'src/main/assets/torrc'
}

task copyResources(type: Copy, dependsOn: deleteSourceFiles) {
    from "../universal/commonResources/torrc"
    from "../universal/commonResources/geoip"
    from "../universal/commonResources/geoip6"
    into 'src/main/assets'
}

// I tried to depend on assemble directly and when I would run debug tests the dependency didn't work right
preBuild.dependsOn(copyResources)

